
# 汇编学习笔记(一)

## 14种寄存器

- 段寄存器
  - ds: *data segment*, 指向存放数据的段
  - cs: *code* ~, 指向存放代码的段, 配合 ip(代码偏移)使用, 自动执行指向的代码
  - ss: *stack* ~, 指向栈(并没有实际的栈, 只是人为规定一块内存为栈), 配合 sp 使用, 可自动进行 push / pop 操作
  - es: *extra* ~, 附加数据寄存器, 用来顶替其他寄存器
- 控制寄存器
  - ip: *instruction pointer*, 配合 cs 使用, 指向谁执行谁, 可以用 jmp 改变
  - flags: 标志寄存器 // TODO
- 通用寄存器
  - 数据寄存器
    - ax: *accumulator*, 常用于存放算术、逻辑运算中的操作数或结果(就是加减法的工具人)
    - bx: *base*, 常用来存放访问内存时的地址, 可用进行取下标操作
    - cx: *count*, 在循环、串操作指令中用作计数器
    - dx: *data*, 在寄存器间接寻址中的I/O指令中存放I/O端口的地址, 此外还在除法时用到他
  - 指针寄存器
    - sp: *stack pointer*, 指向**栈底**
    - bp: *base*, 只要在方括号中用到了 bp 寻址, 段地址默认为 ss, 而不是 ds
  - 变址寄存器
    - si: *source index*, 通常在指针/ 数组操作里指向数据源, 可与 bx 相加进行二维数组操作
    - di: *destination ~*, 同上, 指向目的地

## 基本知识

**段并不是真实存在的**, 而是人为规定的, 便于表示的代码区间, 段寄存器按16进制左移一位(x16)+段偏移即为真实地址, 有多种方法指向同一个物理地址.  
段偏移有16位, 最多偏移 64k

汇编中有 3 种数据表示:

1. 立即数(idata), 存放在cpu里的指令缓冲器里
2. 寄存器数
3. 段地址+偏移地址联合表示

cpu可以且只能以 **字** 或者 **字节** 为单位进行运算, 指明方法有以下几种

```bash
mov ax, ds:[1]              ; 这里ax已经是字型了, cpu 会自动推测
mov word ptr ds:[0], 1H     ; 这里需要指明操作数为 (word|byte) ptr
push 666H                   ; push / pop 无需且不能指定, 就是字型数据
```

内存中的数据是以小端模式存的, ```1234H, 5678H``` 存入内存会变成 ```34 12 78 56```

## 寻址

获取*内存里存放的值*的**地址**的几种方式

1. ```[idata]```: 相当于 ```ds:[idata]```
2. ```[bx]```: 相当于 ```ds:[bx]```
3. ```[bx+idata]```: 在 idata 的基础上往后寻址, 还可以这样写以用于结构体: ```[bx].idata```
4. ```[bx+si]```: 可进行二维数组的操作, 可以写成```[bx][si]```
5. ```[bx+si+idata]```: 花式组合, 可以写成```[bx].idata[si], idata[bx][si]```

只有 **bx, bp, si, di** 可以用于在中括号里寻址, 且他们只有 4 种正确的组合方式:

1. bx + si
2. bx + di
3. bp + si
4. bp + di

通常用 bx 定位整个结构体, 用 idata 定位结构体中的某一项, 用 si 定位数据项中的每个元素, 如```[bx].idata[si] == student.ID[i]```


## debug常用命令

- ```r```: 查看所有寄存器状态
- ```r <寄存器> <回车> <4位16进制>```: 修改寄存器的值, 并查看修改后状态
- ```a <段地址>:<偏移>```: 从这个地址开始输入汇编命令
- ```d <段地址>:<偏移>```: 查看从此开始的内存
- ```t```: 单步执行此时cs:ip指向的程序
- ```p```: 跳出循环 / 在 int 处结束程序
- ```g <地址>```: 执行到指定地址并暂停
- ```e <地址> [值列表, 空格分隔]```: 从地址开始将内存设置为列表的值(字为单位)
- ```u <地址>```: 查看从此地址开始的汇编指令

## 除法

除数: 8 / 16 位, 放在寄存器或者内存中
被除数: 放在 ax 或 (ax 和 dx) 中(如果除数是16位就要借用dx)

```c
div byte ptr ds:[1]
; 等价于
(al) = (ax)/((ds)*10H+1)的商
(ah) = (ax)/((ds)*10H+1)的余数

div word ptr ds:[1]
; 等价于
(ax) = ((dx)*10000H+(ax)) / ((ds)*10H+1)的商
(dx) = ((dx)*10000H+(ax)) / ((ds)*10H+1)的余数
```

## 示例代码

### 将一个菜单里的单词改为大写

```c
; 指定代码段, 数据段, 栈段的名称
assume cs:codesg, ds:datasg, ss:stacksg

datasg segment              ; 开始数据段
    db '1. open '           ; db: data byte, 按字节存入数据
    db '2. help '           ; 这几块内存会依次往后存, 每句话8字节
    db '3. quit '
datasg ends                 ; 声明结束数据段

stacksg segment stack       ; 给栈分配8字节的空间
    db 4 dup(0, 1)          ; <单位> <次数> dup([重复的内容])
                            ; 命令可以复制括号里的内容n次, 以指定单位存放
stacksg ends

codesg segment              ; 声明开始代码段
start:  mov ax, datasg      ; start: 声明程序的入口
                            ; 名字随便取, 和end一致即可
        mov ds, ax          ; (包括上一句)指定代码段的地址
                            ; 段寄存器不能直接赋值
        mov ax, stacksg     ; 指定栈段地址
        mov ss, ax
        mov sp, 8           ; 指向栈底, 
                            ; 由于此时栈为空, 入栈是先将地址-2再存两字节数据
                            ; 所以此时的地址应该是 <栈大小> , 即 栈结束地址(7)+1
        mov bx, 0

        mov cx, 3           ; 指定循环次数, 一共3句数据, 执行3次
    lp: mov si, 0           ; lp: 循环开始的标志, 和loop后的标志相同即可
        push cx             ; 将外层cx入栈保存起来
        mov cx, 4           ; 重新设置内层cx为单词长度
        ilp:    mov al, [bx+3+si]   ; mov al, [bx+3+si]: 
                                    ; 将ds:[bx+3+si]处的值赋给ax的低字节
                                    ; 根据 ax/al/ah 自动推断从ds中取几个字节
                and al, 11011111b   ; 与操作, 转化为大写
                mov [bx+3+si], al   ; 将大写字母赋给ds
                inc si              ; 自增运算
        loop ilp
        pop cx              ; 重新取得外层cx
        add bx, 8           ; 将bx指向下一句开始处 
    loop lp

        mov ax, 4c00h       ; 和下一行一起, 将cpu还给shell
        int 21h
codesg ends         ; 结束代码段

end start           ; 结束整个程序
```

顺便贴一个js一行代码解决的操作...

```js
['1. open ','2. help ','3. quit '].map(it=>it.toUpperCase())
```

一行 vs 四五十行...哎...汇编真是一言难尽啊...

### 计算第一个数除以第二个数, 结果存进第三个数的位置

```c
assume ds: datasg, cs: codesg

datasg segment
    dd 100001   ; define double
    dw 100
    dw 0
datasg ends

codesg segment
start:  mov ax, datasg
        mov ds, ax

        mov ax, ds:[0]
        mov dx, ds:[2]

        div word ptr ds:[4]

        mov ds:[6], ax
codesg ends
end start
```